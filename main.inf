Constant Story "Jack";
Constant Headline "^An Interactive Horror by Jason Lautzenheiser^";
Include "Parser";
Include "Verblib";

!--------------------
!- Player Character
!--------------------
Object myselfobj "(self object)"
  with 
    name 'Jack' 'you' 'body',
    short_name "yourself",
    described,
    hiding,
    description [;
        print "You are very tall.  You are wearing overalls and a flannel shirt.  
        Straw slowly falls from your sleeves and pants leaving a trail
        behind as you walk. ";
        if (self.described == false) {
            print "^^As you raise your hands to your heavy head and move to run 
                your hands through your hair, you feel nothing but a cool, hard 
                shell of a pumpkin.";
            self.described = true;
         };
         print_ret "";
    ],
  has concealed animate proper transparent male;

!--------------------
!- Rooms
!--------------------
Object Neverland "Neverland" 
    with description "should never see this"; 

    Object -> hubcap "hubcap"
        with description
            "Really nothing more than a rusty piece of metal that was loosly 
            attached to the wheel of the car.  It is flat and looks like if 
            you threw it like a frisbee it would go a great distance.";

Object PumpkinPatch "In the Pumpkin Patch"
    with name 'pumpkin patch' 'patch', 
        description
            "You are standing in a middle of a pumpkin patch.   
            The light from the full moon allows you to see some distance 
            around you.  The pumpkin patch goes on in all directions except 
            to the south where you can make the outline of a road.",
        s_to Road,
    has light;

Object Road "Road"    
    with description [;
            if (myselfobj.hiding)
                print "You are lying in the ditch along side the road.  
                    In the darkness, you probably look like nothing more than another pumpkin 
                    amongst thousands.^";
            else 
                print "You are standing in the middle of a dirt road to the south of a
                    large field of pumpkins.  Along the north side of the road is a shallow ditch.  
                    The road stretches on to the east and west.^";
        ],
        n_to PumpkinPatch,
        cant_go [;
            if (myselfobj.hiding) {
                myselfobj.hiding = false;
                "You get out of the ditch and are now standing in the road again.";
            } else {
                "^You feel a strong urge to head towards the town in the east.";
            }
        ],
        before [;
            Go:
                if (myselfobj.hiding) {
                    myselfobj.hiding = false;
                    print "You get up out of the ditch first.^";
                    rfalse;
                }
        ],
        each_turn [;
            StartDaemon(self);
        ],
        turns_active,
        daemon [;
            if (myselfobj.hiding)
            {
                self.turns_active=0; StopDaemon(self);
                myselfobj.hiding = false;
                move hubcap to myselfobj;
                print "^The car drives slowly down the road just feet from where you are lying down.
                  Two boys hang out the side window with shotguns at the ready.^^You hear one of 
                  them say 'I swear I saw something standing around here somewhere.'^^'You just had 
                  too much beer before we left, Joe.  You're so drunk you wouldn't notice old 
                  Jack standing right next to you,'  someone yelled from inside the car.^^The car 
                  slides a bit further down the road, turns around and then heads back the way it 
                  came.^^As the car flies back up the road one of its hubcaps comes off and lands 
                  in the ditch just ahead of you.  You reach over and pick it up.^";
                rtrue;
            };
            switch (++(self.turns_active)) {
                1: if (location == Road) {
                        "^Looking up the road, you see a set of headlights
                        headed in your direction.  Something screams in your very soul that you need to hide,
                        you don't think it would be wise to be caught in the open in your state.";
                    };

                2: if (location == Road) {
                        "^The headlights are coming closer and you can now hear the 
                        low rumble of a car with a rusty exhaust.";
                    } else if (location == PumpkinPatch) {
                        "^From the road you can hear the low rumble of a rusty exhaust as 
                        a car comes closer.";
                    };

                3: if (location == Road) {
                        "^The car's headlights are looming large and you can hear 
                        the passengers in the car hollering 'There he is...There's old Jack.'";
                    } else if (location == PumpkinPatch) {
                        "^The sound of the car is getting louder and you begin to hear the 
                        sounds of young men hollering.";
                    };

                4:  deadflag = 3;
                    if (location == Road) {
                        "^As the car gets closer to you it suddenly accelerates.  
                        Before you can react, it barrels into you.  Your body, 
                        made mostly of straw flys apart scattering straw everywhere. 
                        The last thing you remember before the light dims from your 
                        eyes is your large pumpkin head bouncing off the windshield 
                        and onto the road shattering into thousands of pieces.";
                    } else if (location == PumpkinPatch) {
                        "^As the car slows down you hear someone yell, 'There he is....
                        old Jack is still in the pumpkin patch.'  You hear the sound of
                        gun fire and something whistles pass your head.  You take off 
                        running as more shots whizz close.  Just as you think you may 
                        get away, one slams into the back of your head.  As the light 
                        dims from your eyes, your last thought was 'Next year....'.";
                    };
            }
        ],
    has light;

    Object -> Ditch "ditch"
        with name "ditch", 
            description
                "The ditch is very shallow, not much more than an light 
                impression along the road.  Pumpkin vines from the field 
                have encrouched slightly into the ditch.",
        has container enterable open scenery light;



!--------------------
!- Routines
!--------------------
[ DeathMessage;
      if (deadflag == 3) 
          print "Thus ends this halloween.....the town wins again and 
          poor Jack Halloween waits another year for his revenge upon 
          the town";
];

[Initialise;
    location = PumpkinPatch;
    player = myselfobj;
    "Suddenly awareness comes to you.  You look around slowly, 
    seeing nothing at first, then in the moon light you find 
    yourself in the middle of a pumpkin patch.   Looking down at
    your feet there is straw coming out of the bottom of your 
    pants and falling over your boots.  In wonderment you notice 
    your hands for the first time and see straw coming out of your 
    sleeves as well.  As you stare at your hands you notice a 
    strange glow seemingly coming from within you.  You start to run 
    your fingers through your hair only to realize you have none.  
    As you stand there in shock over what you've become you realize you 
    have nothing but vague memories of a time before the pumpkin patch.  
    You look around and see large pumpkins that seem to have the same 
    shape as your head.  It all comes flooding back to you.....
    you are Jack....Jack Halloween. ";
];

Include "Grammar"; 

!--------------------
!- New Verbs
!--------------------
Verb 'hide' 
    * -> Hide
    * container -> Hide
    * 'in' container -> Hide;


Verb 'lay'
    * 'in' container -> Hide
    * 'down' 'in' container -> Hide
    * 'down' -> Hide;

Extend 'lie' first
    * 'down' -> Hide
    * 'in' container -> Hide
    * 'down' 'in' container -> Hide;

[HideSub; 
    if (myselfobj.hiding) {
        "You are already hiding.";
    }

    if (location ~= Road) {
        if (noun == nothing) {
            "You lie down for a bit but you feel something drawing
            you onward so you quickly get up.";
        }
    }
    else  {
        myselfobj.hiding = true;
        "You lie down in the ditch along the road.";
    }
];

Verb 'xyzzy' 'plugh' 'frotz' 'plover' 'trizbort' * -> Xyzzy;
[ XyzzySub; "Though you can't speak, a rumble arises from deep within 
your chest.  The glow coming from your eyes briefly increases then just 
as quickly fades away."; ];
