!% +include_path=C:\apps\Inform6\inform6lib
!% -s
!% -X
Constant Story "Jack";
Constant Headline "^An Interactive Horror by Jason Lautzenheiser^";
Release 2;
! Statusline time;

Include "Parser";
Replace ScoreSub;

[ ScoreSub;
  if (deadflag == 0)
    "There is no score in this game.";
];

Object LibraryMessages
    with before[;
        Miscellany: if (lm_n == 10) 
                        "What's that? Speak up, speak up.";
        Sorry: "You mumble something apologetic under your breath.";
    ];

Include "Verblib";

!- Constants and Globals
Global p_past_road_car = false;
Global p_past_edge_of_town = false;

!- Classes
Class ItemsThatCanBeHidden
    with hidden false,
        HideMe;

Class Items
    class ItemsThatCanBeHidden;

Class Enemy
    class ItemsThatCanBeHidden,
    with name "enemy" "teen" "man" "young",
        unconscious false,
        dead false,
        before [;
            Attack: if (self.dead) {
                    print_ret "He's already dead, there's nothing else you could really do to him now.";
                } else if (self.unconscious) {
                    print_ret "You may appear to be a monster, but attacking ",(the) noun, " when he
                    is unconscious would serve no purpose.";
                } else {
                    rfalse;
                }
        ],
        Incapacitated [; 
            if (self.dead || self.unconscious) rtrue; rfalse;
        ];

Class OffScreen;

Class Place
    has light;

Class Outdoors
    class Place
    with
        before [;
            Examine: if (noun ofclass CompassDirection ) {
                if (noun == u_obj) {
                    "Above you is a cloudless sky lit by the full moon.";
                } else  if (noun == d_obj) {
                    "You stare at the ground and feel an urge to just lie down and let it end, but something far stronger is pulling you onward.";
                } else {
                    "There is nothing interesting in that direction.";
                }
            };
        ],
    has light;     


! Class Indoors
!     class Place;

Class ScopedWhenCarried
    class ItemsThatCanBeHidden;

!- Player Character
Object myselfobj "(self object)"
  with 
    name 'Jack' 'you' 'body' 'self',
    short_name "yourself",
    described,
    hiding,
    description [;
        print "You are very tall.  You are wearing overalls and a flannel shirt.  
        Straw slowly falls from your sleeves and pants leaving a trail
        behind as you walk. ";
        if (self.described == false) {
            print "^^As you raise your hands to your heavy head and move to run 
                your hands through your hair, you feel nothing but a cool, hard 
                shell of a pumpkin.";
            self.described = true;
         };
         print_ret "";
    ],
    react_before [;
        Disrobe: print "Your clothing is the only thing holding your body of straw together.^";
                rtrue;
    ],
  has concealed animate proper transparent male;

Object -> Overalls "overalls"
    with name 'overalls' 'over alls' 'bibs',
        description "The pair of overalls you are wearing are made of blue denim material and are well worn and ripped at the seams.",
    has worn clothing;

Object -> FlannelShirt "flannel shirt"
    with name 'shirt' 'flannel' 'flannel shirt',
        description "The scotch plaid flannel shirt is loosely tucked into your overalls.",
    has worn clothing;

Object -> Straw "straw"
    with name 'straw' 'stuffing',
        description "The straw seems to be part of your very being covering every part of your body or perhaps it is your body.",
    has worn clothing;



!- Scenery
Class Fardrop
    with
        before [;
            Take: "If you could, you would....but where would you take it?";
            Touch: print_ret (The) noun, " is too far away to touch.";
        ];

Fardrop FullMoon "full moon"
    with
        name 'full' 'moon' 'full moon' 'orb' ,
        found_in [;
           return (location ofclass Outdoors);
        ],
        description 
            "The great orb of the full moon hangs high in the sky.  It reflects enough light 
            in the cloudless sky to provide a clear view of much of the landscape.",
        has scenery light;

!- Rooms
!- Neverland -- used to take items off scene
OffScreen Neverland "Neverland" 
    with description "should never see this"; 

    !- Hubcap starts off in neverland and will end up back there if used to get 
    !- past the young man on the edge of town.
    Items -> hubcap "hubcap"
        with name "hubcap" "cap" "hub" "frisbee",
            description
            "Really nothing more than a rusty piece of metal that was loosly 
            attached to the wheel of the car.  It is flat and looks like if 
            you threw it like a frisbee it would go a great distance.",
        before [;
             ThrowAt: 
                if (real_location ~= EdgeOfTown) {
                    "You wind up and throw the hubcap in the air and sure enough it flies
                    like a frisbee through the night sky.  You must have exceptioanl strength
                    as it flies faster than you could have imagined.  As it falls to the
                    ground, you run over and retrieve it.";
                } else {
                    if (second ofclass Enemy && second.Incapacitated()== false) {
                        second.unconscious = true;
                        print "You quickly stand up and throw the hubcap at the young man.
                            He freezes as he sees your movement and you watch as the hubcap ";
                        if (random(3) ~= 3) {
                            second.dead = true;
                            print "hit him square in the forehead ";
                        } else {
                            print "skips off the top of his head ";
                        } 
                        second.AfterDeath();
                        move hubcap to real_location;
                        rtrue;
                    } else {
                        move hubcap to Neverland;
                        move Patroller to Neverland;
                        p_past_edge_of_town = true;
                        player.hiding = false;
                        "As quitely as you can, you toss the hubcap into the field next to the road.
                        A loud cling rings through the still night as it strikes a rock that juts from the middle
                        of the field.  The man stops in his tracks, looks in the direction of the noise and
                        then takes off running.^^'I've found him  boys.  He's over here.' and disappears in the darkness.";
                    }
                }
            ];
        
        

!- Pumpkin patch -- this is starting room, not much here other that to set the 
!-                  story and atmosphere.
Outdoors PumpkinPatch "In the Pumpkin Patch"
    with name 'pumpkin patch' 'patch', 
        description
            "You are standing in a middle of a pumpkin patch.   
            The light from the full moon allows you to see some distance 
            around you.  The pumpkin patch goes on in all directions except 
            to the south where you can make the outline of a road.",
        s_to Road,
    has light;

!- Road - First area where you start to see what you are up against.
Outdoors Road "Road"    
    with
        headLightsExamined,
        description [;
            if (Road hasnt visited) {
                StartDaemon(self);
            };
            if (myselfobj.hiding)
                print "You are lying in the ditch along side the road.  
                    In the darkness, you probably look like nothing more than another pumpkin 
                    amongst thousands.^";
            else 
                print "You are standing in the middle of a dirt road to the south of a
                    large field of pumpkins.  Along the north side of the road is a shallow ditch.  
                    The road stretches on to the east and west.^";
        ],
        n_to PumpkinPatch,
        e_to EdgeOfTown,
        cant_go [;
            if (myselfobj.hiding) {
                myselfobj.hiding = false;
                "You get out of the ditch and are now standing in the road again.";
            } else {
                "^You feel a strong urge to head towards the town in the east.";
            }
        ],
        before [;
            Go:
                if (myselfobj.hiding) {
                    myselfobj.hiding = false;
                    print "You get up out of the ditch first.^";
                    rfalse;
                };
                if (noun == e_to && p_past_road_car ~= true) {
                    deadflag = 3;
                    print "As you begin to head east towards the town, the car suddenly
                     accelerates. Before you can react, it barrels into you.  Your body, 
                     made mostly of straw flys apart scattering straw everywhere.  The last 
                     thing you remember before the light dims from your eyes is your large pumpkin 
                     head bouncing off the windshield and onto the road only shatter 
                     into thousands of pieces.";
                     rtrue;
                }
        ],
        turns_active,
        daemon [;
            if (myselfobj.hiding)
            {
                self.turns_active=0; StopDaemon(self);
                p_past_road_car = true;
                myselfobj.hiding = false;
                move hubcap to myselfobj;
                print "^The car drives slowly down the road just feet from where you are lying down.
                  Two boys hang out the side window with shotguns at the ready.^^You hear one of 
                  them say 'I swear I saw something standing around here somewhere.'^^'You just had 
                  too much beer before we left, Joe.  You're so drunk you wouldn't notice old 
                  Jack standing right next to you,'  someone yelled from inside the car.^^The car 
                  slides a bit further down the road, turns around and then heads back the way it 
                  came.^^As the car flies back up the road one of its hubcaps comes off and lands 
                  in the ditch just ahead of you.  You reach over and pick it up.^";
                rtrue;
            };
            ++(self.turns_active);
            if (self.headLightsExamined) {
                self.headLightsExamined = false;
                rtrue;
            };
            switch (self.turns_active) {
                1: if (location == Road) {
                        "^Looking up the road, you see a set of headlights
                        headed in your direction.  Something screams in your very soul that you need to hide,
                        you don't think it would be wise to be caught in the open in your state.";
                    };

                2: if (location == Road) {
                        "^The headlights are coming closer and you can now hear the 
                        low rumble of a car with a rusty exhaust.";
                    } else if (location == PumpkinPatch) {
                        "^From the road you can hear the low rumble of a rusty exhaust as 
                        a car comes closer.";
                    };

                3: if (location == Road) {
                        "^The car's headlights are looming large and you can hear 
                        the passengers in the car hollering 'There he is...There's old Jack.'";
                    } else if (location == PumpkinPatch) {
                        "^The sound of the car is getting louder and you begin to hear the 
                        sounds of young men hollering.";
                    };

                4:  deadflag = 3;
                    if (location == Road) {
                        "^As the car gets closer to you it suddenly accelerates.  
                        Before you can react, it barrels into you.  Your body, 
                        made mostly of straw flys apart scattering straw everywhere. 
                        The last thing you remember before the light dims from your 
                        eyes is your large pumpkin head bouncing off the windshield 
                        and onto the road shattering into thousands of pieces.";
                    } else if (location == PumpkinPatch) {
                        "^As the car slows down you hear someone yell, 'There he is....
                        old Jack is still in the pumpkin patch.'  You hear the sound of
                        gun fire and something whistles pass your head.  You take off 
                        running as more shots whizz close.  Just as you think you may 
                        get away, one slams into the back of your head.  As the light 
                        dims from your eyes, your last thought was 'Next year....'.";
                    };
            }
        ],
    has light;

    Object -> Headlights "headlights"
        with name 'lights' 'headlights' 'car' 'vehicle',
        description [;
            if (p_past_road_car) {
                "Luckily the car has headed back up the road and it seems safe to head east now.";
            }
            switch (Road.turns_active) {
                1: Road.headLightsExamined = true;
                    "The car is still off in the distance, but it appears to be moving quickly in your direction";
                2: Road.headLightsExamined = true;
                    "The lights are becoming larger and the vehicle is moving quickly down the road.";
                3: if (player.hiding == true)
                    "From your view in the ditch, the headlights are quite bright and leaks over the edge of the ditch
                     and you are slightly illuminated.  You can only hope that they don't see you.";
                   else 
                    "You are frozen in the middle of the road, unable to move as the vehicle is almost on top of you.";
            }
        ],
        has scenery;
            

    Object -> Ditch "ditch"
        with name "ditch", 
            description
                "The ditch is very shallow, not much more than an light 
                impression along the road.  Pumpkin vines from the field 
                have encrouched slightly into the ditch.",
        has container enterable open scenery light;

!- Edge of town
Outdoors EdgeOfTown "Edge of Town"
    with  
    e_to TownSquare,
    w_to Road,
    description [;
        print "You have come to the edge of town.  A tall chainlink fence 
        stretches on either side of the road as far as you can see.  The road continues
        to the east into the center of town.^";
         if (EdgeOfTown hasnt visited) { 
            player.hiding = true;
            "^As you come closer you see some movement near the barrier across the 
            road and you quickly crouch in the shadows not wanting to be seen.  A 
            moment later you see the movement again and you can just make out 
            someone patrolling back and forth across the road.  There is no way to get around 
            him without being seen.  Perhaps you could distract him somehow.";
         };
         if (PickupTruck.location == EdgeOfTown) {
            "There is a pickup truck sitting along the road.";
         }
    ],
    before [;
    Go: if ((noun == e_to || noun == w_to) && p_past_edge_of_town == false) {
            deadflag = 3;
            if (noun == w_to) {
                "You start to walk back the way you came.  Before you get to far, you hear the young man behind you scream, 'There he is....old Jack...I've got him!'  
                With that you barely hear the shot before the pumpkin atop your body explodes into a million pieces.";
            } else if (noun == e_to) {
                "You come out from hiding and begin to approach the young man.  Before you take more than a few steps, he sees you and yells 'I found him....old Jack...he's right here.' 
                He runs at you and before you can move, he embeds the machete into your pumpkin head.";  
            }

        }
    ];

    Object -> Fence "chainlink fence"
        with name 'fence' 'chain' 'chainlink' 'chainlink fence' 'barrier' 'barbwire' 'wire' 'barb wire',
            description "The fence stretches into the darkness on both sides of the road.  All along the 
                top barbwire keeps you from even thinking about going over the top.  The only way into town
                is up the road to the east.",
            before [;
                Climb: if (Patroller.Incapacitated()) {
                    "You start climbing the fence, but when you reach the top you quickly realize that you'll never
                    make it over the barbwire that lines the top.";
                } else {
                    "You begin to creep towards the fence but almost immediately the young man stops and quickly starts to look around
                    searching for the sound he thinks he heard.  You quickly retreat to your hiding place.";
                };
            ],
            
        has scenery;

    !- Pickup truck
    Object -> PickupTruck "pickup truck"
        with name "truck" "pickup" "ford" "car" "vehicle" "blue" "rusty",
            description "The rusty blue Ford truck sitting along the edge of the road has 
                seen better days.",
        has static;

    Object -> -> TruckBed "truck bed"
        with name 'bed' 'truck bed',
        has open container;

    Object -> -> -> Shovel "shovel"
        with name 'shovel' 'spade' 'garden' 'garden shovel',
            description "The garden shovel has some fresh dirt on it.";

    !- Patrolller
    Enemy -> Patroller "young man"
        with name "man" "teen" "young man",
            AfterDeath [;
                move machete to parent(self);
                move Rifle to parent(self);
                p_past_edge_of_town = true;
                "and he falls to the ground dropping his rifle and machete.";
            ],
            description [;
                if (Patroller.hidden) {
                    "He is hidden among the tall grass in the field where noone coming down the road will see him.";
                } else {
                    print "He is just a young boy, perhaps in his early teens.  ";
                    if (self.unconscious == false) {
                        print "In one hand he carries a large machete and slung from one shoulder is a rifle.";
                    } 
                    if (self.unconscious) {
                        print "He is laying on the ground with a large lump on his head.  He appears to still be
                            breathing so he may come to at any moment so you may want to hurry.";
                    } else if (self.dead) {
                        print "Embedded in his head is the hubcap you just threw.";
                    }
                }
            ],
            before [;
                Take, Pull, Push: 
                if (self.hidden) {
                    "It's best if you just leave him hidden for now.";
                }
                if (self.Incapacitated()) {
                    Patroller.hidden = true;
                    give Patroller scenery;
                    "You drag the body of the young man off into the field where it's hidden from anyone coming down the road.";
                } else {
                    "You think twice of grabbing the young man when you realize what the machete could do to your head.";
                };
                Attack: if (parent(hubcap) == player) 
                            <<ThrowAt hubcap Patroller>>;
                        else 
                            <<Take Patroller>>;
            ],
            HideMe [;
                <<Take self>>;
            ]
        has male;

        ScopedWhenCarried -> -> Machete "machete"
            with name 'machete' 'sword' 'knife',
                description [;
                    if (parent(self) ofclass Enemy) {
                        "The young man holds the machete in his right hand slightly out in front of him.";
                    } else  {
                        "A rusty machete, but still sharp enough it could have sliced through your pumpkin head.l";
                    }
                ];

        
        ScopedWhenCarried -> -> Rifle "rifle"
            with name 'rifle' 'gun',
                description [; 
                    if (parent(self) ofclass Enemy) {
                        print_ret "The gun is slung over the young man's shoulder.";
                    } else {
                        print_ret "A .270 Winchester rifle.  Powerful enough to spread the pumpkin seeds in your head all over the road.";
                    }
                ];

        
Outdoors TownSquare "Town Square"
    with
        w_to EdgeOfTown, 
        description [;
            if (self hasnt visited) {
                player.hiding = true;
                shooter.shot_count++;
                shooter.turns_active = 0;
                StartDaemon(Shooter);
                "As you enter the town's center you hear a gunshot ring out.  The whistle of the slug as it passes near your head and the dust kicking up a 
                few feet behind you are enough to send you scrambling to hide behind the fountain.";
            };
            if (player.hiding) {
                print "You are hiding behind the [fountain] in the town square.  ";
            } else {
                print "You are standing in the center of town.  ";
            };
            "You can head back out of town to the west.  There is an old church off to the north and the townhall building is on the south side of the square. 
            Rising above the townhall is a large clock tower.";
        ],
        cant_go [;
            print "There is nothing in that direction. ";
            if (shooter.dead == false && player.hiding) print "You really shouldn't leave your hiding spot if you have nowhere to go.^";
            rfalse;
        ],
        before [;
            Go: if (noun == n_to || noun == w_to) {
                    if (shooter.reloading == false && shooter.dead == false) {
                        deadflag = 3;
                        "You rise up from your hiding place and attempt to run away, but before you take more than a couple steps, a slug slams into the side of your
                        head spreading pumpkin slime and seeds all over the square.";
                    }
                } else {
                    rfalse;
                };
        ];


    Enemy ->  Shooter "shooter"
            with name 'man' 'shooter' 'assassin' 'sniper' 'sam',
            shot_count,
            turns_active,
            reloading,
            hiding,
            description [;
                print "The shooter is too far away to get a good look at in this poor lighting, but you can see that he ";
                if (self.hiding)
                    "You can't see where the shot came from.";
                else 
                    if (shooter.dead) 
                        "is slumped over the edge of the clock tower presumably dead.";
                    else 
                        "has is rifle aimed in your general direction.";
            ],
        daemon [;
            if (self.dead) {
                self.turns_active = 0; StopDaemon();
            } else {
                ++self.turns_active;
                if (self.turns_active > 1) { 
                    self.reloading = false;
                    if (self.shot_count >= 1) {
                        if (random(100) < 40) {
                            print "^Another gunshot rings through the square. "; 
                            self.shot_count++;
                            print (string) random(
                                "The slug skips off the edge of the fountain near where you are hiding.  You're not sure if it was just a lucky shot or if he actually sees you.^",        
                                "The shot hits something on the other side of the square.^",
                                "Someone from inside a nearby building yells 'Did you get him Sam?'^",  
                                "The shot skips off the water in the fountain.^"  
                            );
                            if (self.shot_count % 6 == 0) {
                                self.reloading = true;
                                print "^You hear the click of an empty rifle and then the sniper fumbling around trying to quickly reload.   This may be your chance.^";
                            }
                        }
                    }
                }
            }
        ],
        has animate male concealed;

!- Standard Routines
[ DeathMessage;
      if (deadflag == 3) 
          print "Thus ends this halloween.....the town wins again and 
          poor Jack Halloween waits another year for his revenge upon 
          the town";
];

!- puts items that are carried by the Patroller in scope if the item has the class of ScopeWhenCarried
[ InScope person i;
  if (person == player && real_location == EdgeOfTown && Patroller.Incapacitated() == false)
      objectloop (i in Patroller)
          if (i ofclass ScopedWhenCarried) PlaceInScope(i);
  rfalse;
];

[ChooseObjects obj code;
    if (code < 2) { 
        if (obj ofclass Enemy) {
            return 2;
        }
    }
    rfalse;
];

!- Initialize function
[Initialise;
    location = PumpkinPatch;
    player = myselfobj;
    SetTime(1380,1);
    print "Suddenly awareness comes to you.  You look around slowly, 
    seeing nothing at first, then in the moon light you find 
    yourself in the middle of a pumpkin patch.   Looking down at
    your feet there is straw coming out of the bottom of your 
    pants and falling over your boots.  In wonderment you notice 
    your hands for the first time and see straw coming out of your 
    sleeves as well.  As you stare at your hands you notice a 
    strange glow seemingly coming from within you.  You start to run 
    your fingers through your hair only to realize you have none.  
    As you stand there in shock over what you've become you realize you 
    have nothing but vague memories of a time before the pumpkin patch.  
    You look around and see large pumpkins that seem to have the same 
    shape as your head.  It all comes flooding back to you.....
    you are Jack....Jack Halloween.^"; 
    banner();
    print "Copyright (c) 2020  - Shrieking Falcon Games^";
    return 2;
];

Include "Grammar"; 

!- New Verbs

!-- Hide
Verb 'hide' 
    * -> Hide
    * container -> Hide
    * 'in' container -> Hide
    * noun -> HideItem;

Verb 'lay'
    * 'in' container -> Hide
    * 'down' 'in' container -> Hide
    * 'down' -> Hide;

Extend 'lie' first
    * 'down' -> Hide
    * 'in' container -> Hide
    * 'down' 'in' container -> Hide;

[HideItemSub;
    if (noun.HideMe) {
        noun.HideMe();
    } else {
        "Not sure how you can hide something like that.";
    }    
];

[HideSub; 
    if (myselfobj.hiding) {
        "You are already hiding.";
    }

    if (location ~= Road) {
        if (noun == nothing) {
            "You lie down for a bit but you feel something drawing
            you onward so you quickly get up.";
        }
    }
    else  {
        myselfobj.hiding = true;
        "You lie down in the ditch along the road.";
    }
];

!-- About

Verb 'about' 'info' 'credits' 
    * -> about;

[ AboutSub; "Jack was created originally for the EctoComp13 and was created in under 3 hours in Inform 7.  
The first release was quickly beta-tested by my son Trey Lautzenheiser, who doesn't normally play IF, but offered some valuable advice.  
While it's taken me a few years, in 2020 I expanded the game and hopefully took out some of the rough edges as well as converted the code to Inform 6.  
Hope you enjoy.^^It may be distributed for free, but not sold or included in any for-profit collection without written permission from the author.^^	
Any bug reports or suggestions are welcome and can be emailed to lautzenheiser.jason@@64gmail.com.^^Check out my IF related blog at lautzofif.wordpress.com."; ];

!-- xyzzy

Verb 'xyzzy' 'plugh' 'frotz' 'plover' 'trizbort' * -> Xyzzy;

[ XyzzySub; "Though you can't speak, a rumble arises from deep within 
your chest.  The glow coming from your eyes briefly increases then just 
as quickly fades away."; ];
